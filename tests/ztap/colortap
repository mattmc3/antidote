#!/usr/bin/awk -f

# Take TAP output and colorize it

# https://testanything.org
BEGIN {
    BOLD="\033[01m"
    BLACK="\033[30m"
    RED="\033[31m"
    GREEN="\033[32m"
    YELLOW="\033[33m"
    BLUE="\033[34m"
    MAGENTA="\033[35m"
    CYAN="\033[36m"
    WHITE="\033[37m"
    BRBLACK="\033[90m"
    BRRED="\033[91m"
    BRGREEN="\033[92m"
    BRYELLOW="\033[93m"
    NORMAL="\033[0m"
}
/^ok /          { print GREEN $0 NORMAL; next }
/^not ok /      { print RED $0 NORMAL; next }
/^Bail out! /   { print MAGENTA $0 NORMAL; next }
/^# ===/        { print BLUE $0 NORMAL; next }
/^# ###/        { print CYAN $0 NORMAL; next }
/^# ok$/        { print BOLD BRGREEN $0 NORMAL; next }
/^# pass /      { print BOLD BRGREEN $0 NORMAL; next }
/^# fail /      { print BOLD BRRED $0 NORMAL; next }
/^# FAIL: /     { print BOLD BRRED $0 NORMAL; next }
/^# warnings /  { print BOLD BRYELLOW $0 NORMAL; next }
/^# WARNING: /  { print BOLD BRYELLOW $0 NORMAL; next }
/^# !!! /       { print MAGENTA $0 NORMAL; next }
/^# stderr: /   { print YELLOW $0 NORMAL; next }
/^#/            { print BRBLACK $0 NORMAL; next }
                { print $0 }
