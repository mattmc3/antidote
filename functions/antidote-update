#!/usr/bin/env zsh

### Update antidote and its cloned bundles.
###
### usage: antidote update [-h|--help]

emulate -L zsh
setopt local_options extended_glob no_notify no_monitor
0=${(%):-%x}

local opts
zparseopts -A opts -D -F -- h -help || return 1

if [[ -v opts[-h] ]] || [[ -v opts[--help] ]]; then
  antidote-help bundle
  return
fi

function __antidote_update {
  bundledir="$1"
  local url=$(git -C "$bundledir" config remote.origin.url)
  local oldsha=$(git -C "$bundledir" rev-parse --short HEAD)
  echo "antidote: checking for updates: $url"
  git -C "$bundledir" pull --quiet --ff --rebase --autostash
  local newsha=$(git -C "$bundledir" rev-parse --short HEAD)
  if [[ $oldsha != $newsha ]]; then
    echo "antidote: updated: $url $oldsha -> $newsha"
  fi
}

# update antidote
antidote selfupdate

echo "Updating bundles..."
local ANTIDOTE_HOME=$(antidote-home)
local d
for d in $ANTIDOTE_HOME/**/.git/..; do
  __antidote_update "${d:A}" &
done
wait
