### Update antidote and its cloned bundles.
###
### usage: antidote update [-h|--help]

emulate -L zsh
setopt local_options extended_glob no_monitor
0=${(%):-%x}

local o_help o_self o_bundles
zparseopts -D -M --       \
  h=o_help    -help=h     \
  s=o_self    -self=s     \
  b=o_bundles -bundles=b  ||
  return 1

if (( $#o_help )); then
  antidote-help update
  return
fi

# update antidote
if (( $#o_self )) || ! (( $#o_bundles )); then
  echo "Updating antidote..."
  git -C "${0:A:h:h}" pull --ff --rebase --autostash
fi

if (( $#o_bundles )) || ! (( $#o_self )); then
  echo "Updating bundles..."
  local d bundledir url oldsha newsha
  for d in $(__antidote_bundles); do
    () {
      bundledir="$1"
      url=$(git -C "$bundledir" config remote.origin.url)
      oldsha=$(git -C "$bundledir" rev-parse --short HEAD)
      echo "antidote: checking for updates: $url"
      git -C "$bundledir" pull --quiet --ff --rebase --autostash
      newsha=$(git -C "$bundledir" rev-parse --short HEAD)
      if [[ $oldsha != $newsha ]]; then
        echo "antidote: updated: $url $oldsha -> $newsha"
      fi
    } "$d" &
  done
  wait
fi

# vim: ft=zsh
