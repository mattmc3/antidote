#!/usr/bin/env zsh

### Print where antidote is cloning bundles.
###
### usage: antidote home [-h|--help]

# Can be overridden by setting `$ANTIDOTE_HOME`.

emulate -L zsh
setopt local_options extended_glob

local o_help
zparseopts -D -M -- h=o_help -help=h || return 1

if (( $#o_help )); then
  antidote-help home
  return
fi

local rootdir
if [[ -n "$ANTIDOTE_HOME" ]]; then
  REPLY=$ANTIDOTE_HOME
elif [[ "${OSTYPE}" == darwin* ]]; then
  REPLY="$HOME/Library/Caches/antidote"
elif [[ "${OSTYPE}" == (cygwin|msys)* ]]; then
  REPLY="$LOCALAPPDATA/antidote"
elif [[ -n "$XDG_CACHE_HOME" ]]; then
  REPLY="$XDG_CACHE_HOME/antidote"
else
  REPLY="$HOME/.cache/antidote"
fi
[[ -d $REPLY ]] || mkdir -p $REPLY
echo $REPLY
