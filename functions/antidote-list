#!/usr/bin/env zsh

### List cloned bundles.
###
### usage: antidote list [-h|--help]

emulate -L zsh
setopt local_options extended_glob

local opts
zparseopts -A opts -D -F -- h -help || return 1

if [[ -v opts[-h] ]] || [[ -v opts[--help] ]]; then
  antidote-help bundle
  return
fi

if [[ $# -ne 0 ]]; then
  echo >&2 "antidote: error: unexpected $1, try --help"
  return 1
fi

local d
local bundle
for d in $(antidote-home)/**/.git/..(N); do
  local url=$(git -C "${d:A}" config remote.origin.url)
  printf '%-64s %s\n' $url ${d:A}
done
