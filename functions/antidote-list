### List cloned bundles.
###
### usage: antidote list [-h|--help]

emulate -L zsh
setopt local_options extended_glob

local o_help o_short o_dirs
zparseopts -D -M --  \
  h=o_help  -help=h  \
  s=o_short -short=s \
  d=o_dirs  -dirs=d  ||
  return 1

if (( $#o_help )); then
  antidote-help list
  return
fi

if [[ $# -ne 0 ]]; then
  echo >&2 "antidote: error: unexpected $1, try --help"
  return 1
fi

local bundles
if zstyle -t ':antidote:bundle' use-friendly-names; then
  bundles=($(antidote-home)/*/*/.git/..(N))
else
  bundles=($(antidote-home)/*/.git/..(N))
fi
bundles=(${(@)bundles:A})

local bundledir
local output=()
for bundledir in $bundles; do
  local url=$(git -C "${bundledir:A}" config remote.origin.url)
  if (( $#o_dirs )); then
    output+=(${bundledir:A})
  elif (( $#o_short )); then
    url=${url%.git}
    url=${url#https://github.com/}
    output+=($url)
  else
    output+=("$(printf '%-64s %s\n' $url ${bundledir:A})")
  fi
done
printf '%s\n' "${(o)output[@]}"

# vim: ft=zsh
