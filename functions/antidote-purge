#!/usr/bin/env zsh

### Remove a cloned bundle.
###
### usage: antidote purge [-h|--help] <bundle>

emulate -L zsh
setopt local_options extended_glob

local opts
zparseopts -A opts -D -F -- h -help || return 1

if [[ -v opts[-h] ]] || [[ -v opts[--help] ]]; then
  antidote-help bundle
  return
fi

if [[ $# -eq 0 ]]; then
  echo >&2 "antidote: error: required argument 'bundle' not provided, try --help"
  return 1
fi

local bundle=$1
local bundledir=$(_antidote_bundledir $bundle)
if [[ ! -d $bundledir ]]; then
  echo >&2 "antidote: error: $bundle does not exist at the expected location: $bundledir"
  return 1
fi

echo "Removing $bundle..."
command rm -rf "$bundledir"

local bundlefile
zstyle -s ':antidote:bundle' file 'bundlefile' || \
  bundlefile=${ZDOTDIR:-~}/.zsh_plugins.txt
echo "removed!"
echo "You may also need to remove $bundle from '$bundlefile'."
