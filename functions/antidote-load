#!/usr/bin/env zsh

### Statically source all bundles from the plugins file.
###
### usage: antidote load [-h|--help] [<bundlefile> [<staticfile>]]

setopt extended_glob

local o_help
zparseopts -D -F -K -- \
  {h,-help}=o_help ||
  return 1

if (( $#o_help )); then
  antidote-help load
  return
fi

local bundlefile=$1
if [[ -z "$bundlefile" ]]; then
  zstyle -s ':antidote:bundle' file \
    'bundlefile' || bundlefile=${ZDOTDIR:-~}/.zsh_plugins.txt
fi
[[ -f "$bundlefile" ]] || touch "$bundlefile"

local staticfile=$2
if [[ -z "$staticfile" ]]; then
  zstyle -s ':antidote:static' file \
    'staticfile' || staticfile=${bundlefile:r}.zsh
fi
[[ "$staticfile" != "$bundlefile" ]] || staticfile="${bundlefile:r}.static.zsh"

# regenerate the static file based on whether the bundle file is newer
[[ $staticfile -nt $bundlefile ]] || antidote bundle <"$bundlefile" >|"$staticfile"
source "$staticfile"
