#!/bin/zsh

### Generate the Zsh script to load a plugin.
#
# usage: antidote script [-h|--help] [-k|--kind <kind>] [-p|--path <path>]
#                        [-c|--conditional <func>] [-b|--branch <branch>]
#                        [-a|--autoload <path>] <bundle>
# <kind>   : zsh,path,fpath,defer,clone,autoload
# <path>   : Relative path from the bundle root
# <branch> : The git branch
# <bundle> : A bundle can be a directory, a zsh script, or a git repo
#

#region private funcs

### Get the path to a plugin's init file.
function __antidote_initfiles {
  emulate -L zsh; setopt $_adote_funcopts
  typeset -ga reply=()
  local dir=${1:A}
  local initfiles=($dir/${dir:A:t}.plugin.zsh(N))
  [[ $#initfiles -gt 0 ]] || initfiles=($dir/*.plugin.zsh(N))
  [[ $#initfiles -gt 0 ]] || initfiles=($dir/*.zsh(N))
  [[ $#initfiles -gt 0 ]] || initfiles=($dir/*.sh(N))
  [[ $#initfiles -gt 0 ]] || initfiles=($dir/*.zsh-theme(N))

  typeset -ga reply=($initfiles)
  printf "%s\n" ${(u)initfiles[@]}
  (( $#initfiles )) || return 1
}

#endregion

function antidote-script {
  emulate -L zsh; setopt $_adote_funcopts
  local MATCH MBEGIN MEND; local -a match mbegin mend  # appease 'warn_create_global'
  local REPLY=

  local o_help o_kind o_path o_branch o_cond o_autoload
  zparseopts $_adote_zparopt_flags -- \
    h=o_help       -help=h       \
    a:=o_autoload  -autoload:=a  \
    b:=o_branch    -branch:=b    \
    k:=o_kind      -kind:=k      \
    p:=o_path      -path:=p      \
    c:=o_cond      -conditional:=c  ||
    return 1

  # set defaults
  (( $#o_kind )) || o_kind=(--kind zsh)

  # strip '=' or ':' from beginning of arg values
  local re='^[=:]?(.+)$'
  [[ $o_kind[-1] =~ $re ]] && o_kind[-1]=$match
  [[ $o_autoload[-1] =~ $re ]] && o_autoload[-1]=$match
  [[ $o_path[-1] =~ $re ]] && o_path[-1]=$match
  [[ $o_cond[-1] =~ $re ]] && o_cond[-1]=$match
  [[ $o_branch[-1] =~ $re ]] && o_branch[-1]=$match

  local supported_kind_vals=(zsh path fpath defer clone autoload)
  if (( $#o_kind )) && ! (( $supported_kind_vals[(Ie)$o_kind[-1]] )); then
    echo >&2 "antidote: error: unexpected kind value: '$o_kind[-1]'" && return 1
  fi

  local bundle=$1
  if [[ -z "$bundle" ]]; then
    echo >&2 "antidote: error: bundle argument expected" && return 1
  fi
  local bundle_name=$(__antidote_bundle_name $bundle)

  # replace ~/ with $HOME/
  if [[ "$bundle" == '~/'* ]]; then
    bundle=$HOME/${bundle#'~/'*}
  fi

  # set the path to the bundle (repo or local)
  local bundle_path
  [[ -e "$bundle" ]] && bundle_path=$bundle || bundle_path=$(__antidote_bundle_dir $bundle)

  # handle cloning repo bundles
  local bundle_type="$(__antidote_bundle_type $bundle)"
  if [[ "$bundle_type" == (repo|url|sshurl) ]] && [[ ! -e "$bundle_path" ]]; then
    local giturl=$(__antidote_tourl $bundle)
    echo >&2 "# antidote cloning $bundle_name..."
    git clone --quiet --depth 1 --recurse-submodules --shallow-submodules $o_branch $giturl $bundle_path
  fi

  # if we only needed to clone the bundle, we're done
  [[ "$o_kind[-1]" != "clone" ]] || return

  # add path to bundle
  [[ -n "$o_path[-1]" ]] && bundle_path+="/$o_path[-1]"

  # handle defer pre-reqs first
  local dopts
  zstyle -s ":antidote:plugin:${bundle}" defer-options 'dopts'
  [[ -n "$dopts" ]] && dopts=" $dopts"

  local -a script=()
  local source_cmd="source"
  if [[ "$o_kind[-1]" == "defer" ]]; then
    source_cmd="zsh-defer${dopts} source"
    script+=(
      'if ! (( $+functions[zsh-defer] )); then'
      "$(antidote-script 'romkatv/zsh-defer' | pr -to 2)"
      'fi'
    )
  fi

  # handle autoloading before sourcing
  if (( $#o_autoload )); then
    script+=(
      "fpath+=( ${bundle_path}/${o_autoload[-1]} )"
      "autoload -Uz \$fpath[-1]/*(N.:t)"
    )
  fi

  # generate load script
  if [[ "$o_kind[-1]" == fpath ]]; then
    script+="fpath+=( $bundle_path )"
  elif [[ "$o_kind[-1]" == path ]]; then
    script+="export PATH=\"$bundle_path:\$PATH\""
  elif [[ "$o_kind[-1]" == autoload ]]; then
    script+=(
      "fpath+=( $bundle_path )"
      "autoload -Uz \$fpath[-1]/*(N.:t)"
    )
  elif [[ -f "$bundle_path" ]]; then
    script+="$source_cmd $bundle_path"
  else
    local initfile initfiles
    initfiles=("${(@f)$(__antidote_initfiles $bundle_path)}")
    script+="fpath+=( $bundle_path )"
    for initfile in $initfiles; do
      script+="$source_cmd $initfile"
    done
  fi

  # mark bundle as loaded
  # script+="zstyle ':antidote:plugin:${bundle_name}' loaded yes"

  # wrap conditional
  if [[ -n "$o_cond[-1]" ]]; then
    echo "if $o_cond[-1]; then"
    printf "  %s\n" $script
    echo "fi"
  else
    printf "%s\n" $script
  fi
}
antidote-script "$@"
