#!/usr/bin/env zsh

### The controller for antidote functionality.

# The reason we use `__antidote` instead putting all of this in `antidote` directly,
# is that this allows the `antidote` command to be overridden via `antidote init`.
# The init command switches antidote from static mode to dynamic mode, but this
# core controller functionality remains.

setopt extended_glob
0=${(%):-%x}

local opts
zparseopts -A opts -D -F -M -- \
  h -help=h \
  v -version=v ||
  return 1

if [[ -v opts[-v] ]] || [[ -v opts[--version] ]]; then
  local ver='1.5.0'
  local gitsha=$(git -C "${0:h:h}" rev-parse --short HEAD 2>/dev/null)
  [[ -z "$gitsha" ]] || ver="$ver ($gitsha)"
  echo "antidote version $ver"

elif [[ -v opts[-h] ]] || [[ -v opts[--help] ]]; then
  antidote-help "$@"

elif [[ $# -eq 0 ]]; then
  antidote-help
  return 2

elif [[ "$1" = help ]]; then
  local manpage=${2:-antidote}
  antidote-help $manpage

elif (( $+functions[antidote-${1}] )); then
  local cmd=$1; shift
  antidote-${cmd} "$@"
  return $?

else
  echo >&2 "antidote: command not found '${1}'" && return 1
fi
